plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'party.52it'
version '1.0.0'

sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

//添加UTF-8编码否则注释可能JAVADOC文档可能生成不了
javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        failOnError false
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.4.0'
    implementation 'org.apache.commons:commons-lang3:3.6'
}

publishing{
    publications{
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'EOSForJava'
                description = 'A EOS library for Java'
                url = 'https://github.com/wypeng2012/EOSForJava'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Jerry Wang'
                        email = 'wypeng2012@gmail.com'
                        organization = '52it'
                        organizationUrl = 'https://52it.party/'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:wypeng2012/EOSForJava.git'
                    developerConnection = 'scm:git:ssh@github.com:wypeng2012/EOSForJava.git'
                    url = 'https://github.com/wypeng2012/EOSForJava'
                }
            }
        }
    }
    repositories{
        maven{
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}